apply plugin: 'com.android.application'

import org.apache.tools.ant.taskdefs.condition.Os

dependencies {
    implementation 'io.github.libpd.android:pd-core:' + rootProject.pdCoreVersion
}

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion
    ndkVersion rootProject.ndkVersion
    namespace = 'org.puredata.android.scenes'

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion 33
        versionCode 11
        versionName "0.9.2"
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    lintOptions {
        ignore 'ExpiredTargetSdkVersion'
    }

    packagingOptions {
        jniLibs {
            // since externals are loaded at native code level, they need to be copied to the actual
            // filesystem, instead of staying in the apk; so we must set useLegacyPackaging:
            useLegacyPackaging true
        }
    }

    buildFeatures {
        // Extract native libs from prefab, to be able to access the headers and to link to the libs
        prefab = true
        viewBinding = true
    }

    defaultConfig {
        externalNativeBuild {
            ndkBuild {
                arguments "NDK_DEBUG=1", "-j" + Runtime.runtime.availableProcessors()
            }
        }
    }

    externalNativeBuild {
        ndkBuild {
            path "jni/Android.mk"
        }
    }
}

